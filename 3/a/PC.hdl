// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    Inc16(in=currentValue, out=incrementedValue);
    Mux16(a=currentValue, b=incrementedValue, sel=inc, out=postIncrementValue);
    Mux16(a=postIncrementValue, b=in, sel=load, out=postLoadValue);
    Mux16(a=postLoadValue, b=false, sel=reset, out=finalValue);
    Register(in=finalValue, load=true, out=out, out=currentValue);

}
